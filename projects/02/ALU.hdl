// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:

    //X is Potentially Zereoed 
    Mux16(a=x,b=false,sel=zx,out=xzx);

    //Y is Potentially Zeroed
    Mux16(a=y,b=false,sel=zy,out=yzy);
    
    //Possibly Negating X
    Not16(in=xzx,out=Nxzx);
    Mux16(a=xzx,b=Nxzx,sel=nx,out=xzxnx);
    
    //Possibly Negating Y
    Not16(in=yzy,out=Nyzy);
    Mux16(a=yzy,b=Nyzy,sel=ny,out=yzyny);

    //Generating Addition Result
    Add16(a=xzxnx,b=yzyny,out=xplusy);

    //Generating And Result
    And16(a=xzxnx,b=yzyny,out=xandy);

    //Picking Output
    Mux16(a=xandy,b=xplusy,sel=f,out=xfy);

    //Generating Negation of Output
    Not16(in=xfy,out=Nxfy);

    //Selecting Output, Preparing Output for Zr Check and Setting Ng
    Mux16(a=xfy,b=Nxfy,sel=no,out=out, out[0..7]=final07,out[8..15]=final815, out[15]=ng);

    //Setting Zr, or all 16 bits, if all zero, then we will get a zero
    //from this process, so we Not it to get a 1
    Or8Way(in=final07,out=or7);
    Or8Way(in=final815,out=or15);
    Or(a=or7,b=or15,out=orAll);
    Not(in=orAll,out=zr);

}   